import std.http.Router.StaticRouter
import std.http.Abstract.RouterAbstractMethod

class Api : StaticRouter, RouterAbstractMethod {


    func group(prefix) {
        let sub = Api()
        sub.routes = this.routes
        sub.pats = this.pats
        sub.middlewares = this.middlewares
        sub.current_prefix = this.current_prefix + prefix
        return sub
    }

    func uses(path, m_func) {
        let fullPath = this.current_prefix + path
        if (this.middlewares[fullPath] == nil) {
            __map_set_manual__(this.middlewares, fullPath, [])
        }
        let list = this.middlewares[fullPath]
        list.push(m_func)
    }

    @override
    func route(path, method, handler) {
        let fullPath = this.current_prefix + path
        if (this.routes[fullPath] == nil) {
            __map_set_manual__(this.routes, fullPath, {})
            this.pats.push(fullPath)
        }
        let target = this.routes[fullPath]
        __map_set_manual__(target, method, handler)
    }

    @override
    func get(path, handler) {
        this.route(path, "GET", handler)
    }

    @override
    func post(path, handler) {
        this.route(path, "POST", handler)
    }

    func run(port) {
        if (__listen__(port)) {
            println("Jackal Server: http://localhost:" + port.toString())
            while (true) {
                let req = __accept__()
                if (req != nil) {
                    let m = req["method"]
                    let p = req["path"]
                    
                    let matchedPat = nil
                    for pat in this.pats {
                        if (__match_route__(req, p, pat)) {
                            matchedPat = pat
                            break
                        }
                    }

                    if (matchedPat != nil) {
                        let m_list = this.middlewares[matchedPat]
                        let allowed = true
                        if (m_list != nil) {
                            for m_func in m_list {
                                let m_res = m_func(req)
                                if (m_res != nil) {
                                    println("[" + m + "] " + p + " -> 401 Unauthorized")
                                    __send_json__(req, m_res)
                                    allowed = false
                                    break
                                }
                            }
                        }

                        if (allowed) {
                            let pathData = this.routes[matchedPat]
                            let handler = pathData[m]
                            if (handler != nil) {
                                println("[" + m + "] " + p + " -> 200 OK")
                                let params = req["params"]
                                if (params != nil) {
                                    __send_json__(req, handler(params["id"]))
                                } else {
                                    __send_json__(req, handler)
                                }
                            } else {
                                println("[" + m + "] " + p + " -> 405 Method Not Allowed")
                                __send_json__(req, {"error": 405})
                            }
                        }
                    } else {
                        println("[" + m + "] " + p + " -> 404 Not Found")
                        __send_json__(req, {"error": 404})
                    }
                }
            }
        }
    }
}